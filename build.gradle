plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'io.github.iherongh'
version = '0.2.0-SNAPSHOT'
description = 'A Minecraft plugin that integrates MediaWiki functionality'

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
        maven { url 'https://oss.sonatype.org/content/groups/public/' }

    }

}

dependencies {
    annotationProcessor "dev.jorel:commandapi-annotations:11.0.0"

    // Paper API
    compileOnly "io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT"

    // CommandAPI
    implementation "dev.jorel:commandapi-paper-shade:11.0.0"

    compileOnly "dev.jorel:commandapi-annotations:11.0.0"

    compileOnly 'io.github.fastily:jwiki:1.11.0'

    compileOnly 'net.luckperms:api:5.4'

}

def targetJavaVersion = 21

java {
    def javaVersion = JavaVersion.toVersion( targetJavaVersion )
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if ( JavaVersion.current() < javaVersion ) {
        toolchain.languageVersion = JavaLanguageVersion.of( targetJavaVersion )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set( targetJavaVersion )

    }

}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching( 'paper-plugin.yml' ) {
        expand props

    }

}

shadowJar {
    relocate( 'dev.jorel.commandapi', 'io.github.iherongh.commandapi')

}
